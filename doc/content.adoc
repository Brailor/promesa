= promesa
Andrey Antukh, <niwi@niwi.nz>
0.3.0
:toc: left
:!numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:
:idseparator: -
:idprefix:


== Introduction

A promise library for ClojureScript that uses
link:https://github.com/petkaantonov/bluebird/[bluebird] behind the scenes.


== Rationale

Before I started writing this library I reviewed the following:

* link:https://github.com/bodil/redlobster[redlobster]
* link:https://github.com/Breezeemr/promise[promise]
* link:https://github.com/jamesmacaulay/cljs-promises[cljs-promises]

And all them have one thing in common, they are not actively maintained.

With the exception of cljs-promises, the other two libraries attempt to emulate the
clojure promise which is based on a blocking api this is not very natural in JavaScript
environments.

Also, the last one, uses a core.async abstraction. This is not bad as core.async is a very
well done library, but it does it in a questionable way in my opinion.

Finally, they all lack good documentation and docstrings.

Instead of reinventing the wheel the *promise* library uses the powerful and very
performant link:https://github.com/petkaantonov/bluebird/[bluebird] JavaScript promise
library and includes it in the package as built in dependency (using ClojureScript
`:foreign-libs` compiler option).

It exposes a lightweight and idiomatic api for almost all methods and functions of the
bluebird library. It also includes syntactic sugar for making the code resemble ES7
async/await syntax.


== Project Maturity

Since _promesa_ is a young project there may be some API breakage.


== Install

This section covers the installation and requirements of the _promesa_ library.


=== Leiningen

The simplest way to use _promesa_ library in a ClojureScript project is by including
it as a dependency in your *_project.clj_*:

[source,clojure]
----
[funcool/promesa "0.3.0"]
----


== User guide

This library also comes with lightweight abstraction for making use of promises in a more
idiomatic way.


=== Creating a promise

Let start creating a promise:

[source, clojure]
----
(require '[promesa.core :as p])

;; Create a fulfilled promise
(p/promise 1)
;; => #<Promise [1]>
----

If a `promise` function receives a plain value, it returns a resolved promise with the
provided plain value. If it receives an instance of `Error`, it returns a rejected
promise.

And finally it also accepts a factory callback that receives two callable parameters:
`resolve` and `reject`. So you can use one or other to resolve or reject the promise
respectively.

[source, clojure]
----
;; Create a promise from callback
(p/promise (fn [resolve reject]
             ;; Do things, maybe async...
             (resolve 1)))
----


=== Chain computations

Here's how you use that promise:

[source, clojure]
----
(-> (p/promise 1)
    (p/then (fn [v] (inc v)))
----

The `then` function takes one argument: a callback that will be called when the promise
is successfully resolved.

But `then` is not the end, it also returns a promise that will be resolved or rejected
depending on return value of the first chain callback. That will allow you to compose
async computations in an easy way.

[source, clojure]
----
(-> (p/promise 1)
    (p/then (fn [v] (inc v)))
    (p/then (fn [v] (* v 2))))
----


=== Error handling

Until now, we have seen resolved promises, but what happens when an error occurs?

[source, clojure]
----
(-> (p/promise (ex-info "error" nil))
    (p/catch (fn [error]
               (.log js/console error))))
----

The `catch` function adds a new handler to the promise chain that will be called when
any of the previous promises in the chain are rejected or an exception is raised.


=== Sequences

Creating the sequence:

[source, clojure]
----
(require '[cats.core :as m])

(let [p (m/>>= (do-something-first)
               do-something-second
               do-something-third)]
  (p/then p (fn [result]
              (.log js/console "Result: " result))))
----

The standard promise library and JavaScript do not offer a standard way of chaining
promises in sequence in that situation we use the _cats_ library `>>=` function.

The _cats_ library offers monad abstractions and helpers to work with them, but do not
worry, you don't need to understand anything related to monads. Just use the `>>=`
function as a helper to chain together a sequence of promises.


=== Parallelism

JavaScript environments like browsers or nodejs are pretty good at parallelizing IO
operations like downloading content, using ajax or reading a file in nodejs.

[source, clojure]
----
(let [p (p/all [(do-some-io)
                (do-some-other-io)])]
  (p/then p (fn [array-of-results]
              (do-something-with-results array-of-results))))
----

There is another helper that allows you to unroll arguments when working with multiple
promises:

[source, clojure]
----
(let [p (p/all [(do-some-io)
                (do-some-other-io)])]
  (p/spread p (fn [result1 result2]
                (do-stuff-with result1 result2))))
----

The `spread` function works like the `then` function, with a unique difference being that
it unrolls the received array of promises as positional arguments to the handler
callback.


=== Delays and timeouts.

JavaScript due its nature, does not allow you to block or sleep. But with promises you
can emulate the functionality using `delay` like so:

[source, clojure]
----
(-> (p/delay 1000 "foobar")
    (p/then (fn [v]
              (.log js/console "Received:" v))))

;; After 1 second it will print the message
;; to the console: "Received: foobar"
----

The promise library offers the ability to add a timeout to async operations thanks
to the `timeout` function:

[source, clojure]
----
(-> (some-async-task)
    (p/timeout 200)
    (p/then #(.log js/console "Task finished"))
    (p/catch #(.log js/console "Timeout")))
----

In case the async task is slow, in the example more that 200ms, the promise will be
rejected with timeout error and successfully captured with the `catch` handler.


=== Choice

You also can choose the first `n` number of promises to be fulfilled before moving on by
using the `some` function:

[source, clojure]
----
(let [p (p/some 2 [(p/delay 100 1)
                   (p/delay 200 2)
                   (p/delay 120 3)])]
  (p/spread p (fn [x y]
                (.log js/console "The first two finished: " x y))))
----

There is a helper function called `any` that works very similar to `some` with
`1` as the first argument. It returns a promise that will be fulfilled with one value
instead of an array with the length of 1.

[source, clojure]
----
(let [p (p/any [(p/delay 100 1)
                (p/delay 200 2)
                (p/delay 120 3)])]
  (p/then p (fn [x]
              (.log js/console "The first one finished: " x))))
----


=== Sugar syntax

Promises are one of the most used primitives for the composition of async functions.
However, using promises _as is_ doesn't save you from callback hell.

The upcoming ES7 standard will introduce syntactic sugar for working with promises in a
less painful way by introducing the new `async` and `await` keywords. You can read more
about them link:http://jakearchibald.com/2014/es7-async-functions/[here] and
link:http://pouchdb.com/2015/03/05/taming-the-async-beast-with-es7.html[here].

Let's see how we cam use similar syntactic sugar in ClojureScript:

[source, clojure]
----
(defn do-stuff []
  (m/mlet [x (p/promise 1)   ;; do async operation
           _ (p/delay 1000)  ;; emulate sleep
           y (p/promise 2)]  ;; do an other async operation
    (+ x y)))                ;; do the operation with results
                             ;; of previous two async operations

(p/then (do-stuff)
        (fn [v] (println v)))
----

In this example we have used the `mlet` macro from the `cats` library.

Behind the scenes, the _promise_ library implements and uses monadic abstractions. And
in this case, the _Promise_ type implements a _Functor_ and _Monad_ abstractions.

This part of the documentation, requires a minimal amount of knowledge about
these concepts. However, it is not mandatory to read and understand this
portion in order to make use of this library.

Previously we have seen the `mlet` macro which is very similar in functionality to the ES7
`async` and `await` functions.

The main difference with the ES7 syntax is that it is not bound only to promises. It is a
generic and extensible abstraction that allows you to work with different types of logic
allowing for composition of computations.

Please, read the link:http://funcool.github.io/cats/latest/[cats documentation] to see
other types and abstractions.




=== Raw access to bluebird api

This library gives you the full access to the fantastic bluebird promise library.

To start using it, you just need to require it like any other ClojureScript ns:

[source, clojure]
----
(ns yourapp.core
  (:require [org.bluebird]))

(def p (js/Promise. (fn [resolve reject]
                      (resolve 1))))
(.then p (fn [v]
           (.log js/console v)))
;; => 1
----


== Developers Guide

=== Contributing

Unlike Clojure and other Clojure contrib libs, does not have many restrictions for
contributions. Just open a issue or pull request.

=== Get the Code

_promesa_ is open source and can be found on
link:https://github.com/funcool/promesa[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/promesa
----

=== Run tests

To run the tests execute the following:

[source, text]
----
lein cljsbuild test
----

You will need to have nodejs or iojs installed on your system.


=== License

_promesa_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2015 Andrey Antukh <niwi@niwi.nz>

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
