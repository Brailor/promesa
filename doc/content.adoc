= promesa - A promise library for ClojureScript.
Andrey Antukh, <niwi@niwi.be>
:toc: left
:numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:


== Introduction

A promise library for ClojureScript that uses link:https://github.com/petkaantonov/bluebird/[bluebird]
behind the scenes.


== Project Maturity

Since _promesa_ is a young project there may be some API breakage.


== Install

This section covers the installation and requirements of the _promesa_ library.


=== Leiningen

The simplest way to use _promesa_ library in a ClojureScript project is by including
it as a dependency in your *_project.clj_*:

[source,clojure]
----
[funcool/promise "0.1.0-SNAPSHOT"]
----


=== Get the Code

_promesa_ is open source and can be found on link:https://github.com/funcool/promesa[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/promesa
----


== User guide

This library also comes with lighweight abstraction for make use promises in a more
idiomatic way.


=== Creating a promise

Let start creating a promise:

[source, clojure]
----
(require '[promesa.core :as p])

;; Create a fulfilled promise
(p/promise 1)

;; Create a promise from callback
(p/promise (fn [resolve reject]
             ;; Do things, maybe async...
             (resolve 1)))
----

The `promise` function is polymorphic. If it receives a plain value, it return a resolved
promise with provided plain value. If it receives an instance of `Error`, it return a rejected
promise.

And finally, if it receives a callback with two parameters, resolve and reject. Do something
within the callback, perhaps async, then call resolve if everything worked, otherwise call reject.


=== Chain computations

Here's how you use that promise:

[source, clojure]
----
(-> (p/promise 1)
    (p/then (fn [v] (inc v)))
----

The `then` function takes one argument: a callback that will be called when the promise
successfully resolved.

But `then` is not the end, it also returns a promise that will resolved or rejected depending
on return value of the first chain callback. That will allow compose async computations
in an easy way.

[source, clojure]
----
(-> (p/promise 1)
    (p/then (fn [v] (inc v)))
    (p/then (fn [v] (* v 2))))
----


=== Error handling

Until now, we have seen resolved promises, but that is happens when an error ocurrs?

[source, clojure]
----
(-> (p/promise (ex-info "error" nil))
    (p/catch (fn [error]
               (.log js/console error))))
----

The `catch` function adds a new handler to the promise chain that will be called when
any of the previous promises in the chain is rejected or an exception is raised.


=== Sequences

Creating the sequence:

[source, clojure]
----
(require '[cats.core :as m])

(let [p (m/>>= (do-something-first)
               (do-something-second)
               (do-something-third))]
  (p/then p (fn [result]
              (.log js/console "Result: " result))))
----

The standard promises library and javascript does not offers a standard way of chaining
promises in sequence and in that situation we use the _cats_ library `>>=` function for it.

The _cats_ library offers a monad abstractios and helpers for work with them, but do not worry,
you don't need understand nothing related to monads. Just use the `>>=` function as helper
for make chain sequences of promises.


=== Parallelism

Javascript environments like browsers or nodejs are pretty good parallelizing IO operations
how download content using ajax or reading a file in nodejs.

[source, clojure]
----
(let [p (p/all [(do-some-io)
                (do-some-other-io)])]
  (p/then p (fn [array-of-results]
              (do-something-with-results array-of-results))))
----

It there an other helper that allos unroll arguments wen working with multiple promises:

[source, clojure]
----
(let [p (p/all [(do-some-io)
                (do-some-other-io)])]
  (p/spread p (fn [result1 result2]
                (do-stuff-with result1 result2))))
----

The `spread` function works like `then` function, with unique diferrence that
it unroll the received array of promises as positional arguments to the handler
callback.


=== Delays and timeouts.

Javascript due its nature, does not can block or sleep. But with promises you can emulate
it using `delay` function:

[source, clojure]
----
(-> (p/delay 1000 "foobar")
    (p/then (fn [v]
              (.log js/console "Received:" v))))

;; After 1 second it will print the message
;; to the console: "Received: foobar"
----

Also, the promise library offers put a timeout to some async operation thanks
to `timeout` function:

[source, clojure]
----
(-> (some-async-task)
    (p/timeout 200)
    (p/then (fn [x]
              (.log js/console "Task finished")))
    (p/catch (fn [_]
               (.log js/console "Timeout"))))
----

In case of the async task is slow more that 200ms, the promise will be rejected
with timeout error and successfuly captured with `catch` handler.


=== Choice

You also can choice a first `n` number of promises that will filfilled first
using the `some` function:

[source, clojure]
----
(let [p (p/some 2 [(p/delay 100 1)
                   (p/delay 200 2)
                   (p/delay 120 3)])]
  (p/spread p (fn [x y]
                (.log js/console "The first two finished: " x y))))
----

It there a helper function called `any` that works very similar to `some` with
`1` as first argument and it return a promise that will fullfilled with one value
instead of array of length 1.

[source, clojure]
----
(let [p (p/any [(p/delay 100 1)
                (p/delay 200 2)
                (p/delay 120 3)])]
  (p/then p (fn [x]
              (.log js/console "The first one finished: " x))))
----

=== Sugar syntax

Promises is one of the most used primitives for make composition of async computations. But using
promises _as is_ not solves you from the callback hell.

The upcoming ES7 standard will introduce a sugar syntax for work with promises in a painless
way, using new `async` y `await` keywords. You can read more about that
link:http://jakearchibald.com/2014/es7-async-functions/[here] and
link:http://pouchdb.com/2015/03/05/taming-the-async-beast-with-es7.html[here].

Let see how we use similar sugar syntax in clojurescript:

[source, clojure]
----
(defn do-stuff []
  (m/mlet [x (p/promise 1)   ;; do async operation
           _ (p/delay 1000)  ;; emulate sleep
           y (p/promise 2)]  ;; do an other async operation
    (+ x y)))                ;; do the operation with results
                             ;; of previous two async operations

(p/then (do-stuff)
        (fn [v] (println v)))
----

In this example we have used the `mlet` macro that comes from `cats` library.


=== Raw access to bluebird api

This library gives you the full access to the fantastic bluebird promise library.

For start using it, just require it like any other clojurescript ns:

[source, clojure]
----
(ns yourapp.core
  (:require [org.bluebird]))

(def p (js/Promise. (fn [resolve reject]
                      (resolve 1))))
(.then p (fn [v]
           (.log js/console v)))
;; => 1
----

== Advanced usage

Behind the scenes, the _promise_ library implements and uses monadic abstractions. And in this
case, the _Promise_ type implements a _Functor_ and _Monad_ abstractions.

This part of documentation, requires a minimal knowledge about that concepts but is not mandatory
to kwnon and read this part for use the library.

Previously we have seen the `mlet` macro that does very similar things to the ES7 `async`
and `await` sugar syntax.

The main difference with ES7 syntax is that is not bound only to promises, is a generic and
extensible abstraction that allows work with different kind of types and different types of
logic for composition of computations.

Please, read the link:http://funcool.github.io/cats/latest/[cats documentation] for see other types and other abstractions.


== Contribute

**promesa** unlike Clojure and other Clojure contrib libs, does not have many
restrictions for contributions. Just follow open a issue or pull request.


== Run tests

For run tests just execute this:

[source, text]
----
lein cljsbuild test
----

You should have nodejs or iojs installed in your system or any node version manager
like **nvm**.


== License

_promesa_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2015 Andrey Antukh <niwi@niwi.be>

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
