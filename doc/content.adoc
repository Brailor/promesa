= promesa - composable promise library for ClojureScript
Andrey Antukh, <niwi@niwi.nz>
0.5.0
:toc: left
:!numbered:
:idseparator: -
:idprefix:
:sectlinks:
:source-highlighter: pygments
:pygments-style: friendly


== Introduction

A promise library for ClojureScript that uses
link:https://github.com/petkaantonov/bluebird/[bluebird] behind the scenes.


=== Rationale

Before I started writing this library I reviewed the following:

* link:https://github.com/bodil/redlobster[redlobster]
* link:https://github.com/Breezeemr/promise[promise]
* link:https://github.com/jamesmacaulay/cljs-promises[cljs-promises]

And all them have one thing in common, they are not actively maintained.

With the exception of cljs-promises, the other two libraries attempt to emulate the
clojure promise which is based on a blocking api this is not very natural in
JavaScript environments.

Also, the last one, uses a core.async abstraction. This is not bad as core.async is
a very well done library, but it does it in a questionable way in my opinion.

Finally, they all lack good documentation and docstrings.

Instead of reinventing the wheel, the *promise* library uses the powerful and very
performant link:https://github.com/petkaantonov/bluebird/[bluebird] JavaScript
promise library and includes it in the package as built in dependency (using
ClojureScript `:foreign-libs` compiler option).

It exposes a lightweight and idiomatic api for almost all methods and functions
of the bluebird library. It also includes syntactic sugar for making the code
resemble ES7 async/await syntax.


=== Project Maturity

Since _promesa_ is a young project there may be some API breakage.


=== Install

The simplest way to use _promesa_ library in a ClojureScript project is by including
it as a dependency in your *_project.clj_*:

[source,clojure]
----
[funcool/promesa "0.5.0"]
----


== User guide

=== Introduction

The promise consists in a container that eventually will contain a value with
builtin support for error handling. So the promise has three different states:

- `resolved`: means that the promise contains a value.
- `rejected`: means thet the promise contains an error.

In summary: is the abstraction that represents the result of an asynchronous
operation that will be eventually available.


=== Creating a promise

It there several different ways to create a promise in _promissum_ library. You can
create it already resolved with initial value or already rejected with an exception.

Let start creating a promise from a plain value:

[source, clojure]
----
(require '[promesa.core :as p])

;; Create a fulfilled promise
(p/promise 1)
;; => #<Promise [1]>
----

If a `promise` function receives a plain value, it returns a resolved promise with
the provided plain value. If it receives an instance of `Error`, it returns a
rejected promise.

And finally it also accepts a factory callback that receives two callable parameters:
`resolve` and `reject`. So you can use one or other to resolve or reject the promise
respectively.

[source, clojure]
----
;; Create a promise from callback
(p/promise (fn [resolve reject]
             ;; Do things, maybe async...
             (resolve 1)))
----


=== Simple chaining

It there different ways to compose/chain computations using promises. We will start
with the basic: lineal way of chaining computations.

That can be done using `then` or `chain` functions exposed in `promissum.core`
namespace. Bot them are mainly interchangeable. The main differencia is that
`chain` is variadic and `then` not.

[source, clojure]
----
(def pr (-> (p/promise 1)
            (p/then inc)
            (p/then inc)
            (p/then inc)))

(p/then pr (fn [v]
             (println v)))
;; => Stdout: 4
----

And here the same example using the `chain` function instead of `then`:

[source, clojure]
----
(def pr (-> (p/promise 1)
            (p/chain inc inc inc))

(p/then pr (fn [v]
             (println v)))
;; => Stdout: 4
----


=== Advanced chaining

Later, thanks to the link:https://github.com/funcool/cats[cats] library, it there
other few methods of create promise compositions in more powerfull way: `mlet`
and `alet` macros and `fmap` function.

For demostration purposes, imagine that you have this function that emulates async
operation and return a promise:

[source, clojure]
----
(require '[cats.core :as m])
(require '[promesa.core :as p])

(defn sleep-promise
  [wait]
  (p/promise (fn [deliver]
               (js/setTimeout #(deliver wait) wait))))
----

The `cats.core/fmap` function allows us apply a function to the value
into the promise and return an other promise with the result:

[source, clojure]
----
(require '[cats.core :as m])

(-> (m/fmap inc (sleep-promise 10))
    (p/then (fn [v]
              (println v))))

;; => Stdout: 11
----

Until now, we have seen a very simple promise compositions that does not depends
on previous results. The `cats.core/mlet` macro allows us build more complex and
more powerfull promise compositions.

Let's see an example using `mlet` macro and additionally messure the execution time:

[source, clojure]
----
(def pr
  (m/mlet [x (sleep-promise 42)
           y (sleep-promise 41)]
    (+ x y)))

(p/then pr (fn [v]
             (println v)))

;; This will take aprox: 83-85 msecs
;; => Stdout: 83
----

The `mlet` bindings are executed sequentially, waiting in each step for promise
resolution. If an error occurs in some step, the entire composition will be
short-circuited, returing exceptionally resolved promise. This is very similar
to the up coming link:http://jakearchibald.com/2014/es7-async-functions/[ES7 async/await], but built in a generic way and as a library and not as language feature.

The main disadvantage of `mlet` is that it's evaluation model is strictly
secuential. It is ok for some use cases, when the sequential order is mandatory.
But, if the strictly secuential model is not mandatory, `mlet` does not take
the advantage of concurrency.

For solve this problem, it there `alet` macro. It is almost identical to `mlet`
from the user experience but internally it is based in very different abstractions.

Now, we will try to do the same example but using the `alet` macro:

[source, clojure]
----
(def pr
  (m/alet [x (sleep-promise 42)
           y (sleep-promise 41)]
    (+ x y)))

(p/then pr (fn [v]
             (println v)))

;; This will take aprox: 41-45 msecs
;; => Stdout: 83
----

We can observe that it returns the same result as in previous example,
but takes almost the half of time to finish execute all the computations. This
is happens because `alet` is more smarter macro and calculates de dependencies
between declared bindings and executes them in batches.

You can read more about that link:http://funcool.github.io/cats/latest/#syntax-sugar[here].


=== Error handling

One of the advantages of using promise abstraction is that it natively has
a notion of error, so you don't need reinvent it. If some of the computations
of the composed promise chain/pipeline raises an exception, that one is
automatically propagated to the last promise making the effect of short-circuiting.

Let see an example:

[source, clojure]
----
(-> (p/promise (ex-info "error" nil))
    (p/catch (fn [error]
               (.log js/console error))))
----

The `catch` function adds a new handler to the promise chain that will be called
when any of the previous promises in the chain are rejected or an exception is
raised. The `catch` function also returns a promise that will be resolved or rejected
depending on that will happen inside the catch hanlder.


=== Branching

For adding both success and error handlers to a promise at the same time you can use
the `branch` function:

[source, clojure]
----
(p/branch a-promise
          (fn [v]
            (.log js/console "Ok" v))
          (fn [err]
            (.error js/console err)))
----


=== Working with collections

In some circumstances you will want wait a completion of few promises at same time,
and _promesa_ also provides helpers for that.

Imagine that you have a collection of promises and you want to wait until
all of them are resolved. This can be done using the `all` combinator:

[source, clojure]
----
(let [p (p/all [(do-some-io)
                (do-some-other-io)])]
  (p/then p (fn [[result1 result2]
              (do-something-with-results result1 result2))))
----

It there are also circumstances where you only want arbitrary select of the
first resolved promise. For this case, you can use the `some` combinator:

[source, clojure]
----
(let [p (p/some 2 [(p/delay 100 1)
                   (p/delay 200 2)
                   (p/delay 120 3)])]
  (p/then p (fn [[x y]]
              (.log js/console "The first two finished: " x y))))
----

There is a helper function called `any` that works very similar to `some` with
`1` as the first argument. It returns a promise that will be fulfilled with one
value instead of an array with the length of 1.

[source, clojure]
----
(let [p (p/any [(p/delay 100 1)
                (p/delay 200 2)
                (p/delay 120 3)])]
  (p/then p (fn [x]
              (.log js/console "The first one finished: " x))))
----


Later, for more advanced use cases, _promesa_ is an algebraic structure that
implements the associative binary operation usually called `mappend`:

[source, clojure]
----
(require '[cats.core :as m])

(def pr (m/mappend (p/promise {:a 1})
                   (p/promise {:b 2})))
(p/then pr (fn [v]
             (println v)))

;; => Stdout: {:a 1 :b 2}
----


=== Delays and timeouts.

JavaScript due its nature, does not allow you to block or sleep. But with promises
you can emulate the functionality using `delay` like so:

[source, clojure]
----
(-> (p/delay 1000 "foobar")
    (p/then (fn [v]
              (.log js/console "Received:" v))))

;; After 1 second it will print the message
;; to the console: "Received: foobar"
----

The promise library offers the ability to add a timeout to async operations thanks
to the `timeout` function:

[source, clojure]
----
(-> (some-async-task)
    (p/timeout 200)
    (p/then #(.log js/console "Task finished"))
    (p/catch #(.log js/console "Timeout")))
----

In case the async task is slow, in the example more that 200ms, the promise will be
rejected with timeout error and successfully captured with the `catch` handler.


=== Sugar syntax

Promises are one of the most used primitives for the composition of async functions.
However, using promises _as is_ doesn't save you from callback hell.


Let's see how we cam use similar syntactic sugar in ClojureScript:

[source, clojure]
----
(defn do-stuff []
  (m/mlet [x (p/promise 1)   ;; do async operation
           _ (p/delay 1000)  ;; emulate sleep
           y (p/promise 2)]  ;; do an other async operation
    (+ x y)))                ;; do the operation with results
                             ;; of previous two async operations

(p/then (do-stuff)
        (fn [v] (println v)))
----

In this example we have used the `mlet` macro from the `cats` library.

Behind the scenes, the _promise_ library implements and uses monadic abstractions. And
in this case, the _Promise_ type implements a _Functor_ and _Monad_ abstractions.

This part of the documentation, requires a minimal amount of knowledge about
these concepts. However, it is not mandatory to read and understand this
portion in order to make use of this library.

Previously we have seen the `mlet` macro which is very similar in functionality to the ES7
`async` and `await` functions.

The main difference with the ES7 syntax is that it is not bound only to promises. It is a
generic and extensible abstraction that allows you to work with different types of logic
allowing for composition of computations.

Please, read the link:http://funcool.github.io/cats/latest/[cats documentation] to see
other types and abstractions.




=== Raw access to bluebird api

This library gives you the full access to the fantastic bluebird promise library.

To start using it, you just need to require it like any other ClojureScript ns:

[source, clojure]
----
(ns yourapp.core
  (:require [org.bluebird]))

(def p (js/Promise. (fn [resolve reject]
                      (resolve 1))))
(.then p (fn [v]
           (.log js/console v)))
;; => 1
----


== Developers Guide

=== Contributing

Unlike Clojure and other Clojure contrib libs, does not have many restrictions for
contributions. Just open a issue or pull request.

=== Get the Code

_promesa_ is open source and can be found on
link:https://github.com/funcool/promesa[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/promesa
----

=== Run tests

To run the tests execute the following:

[source, text]
----
./scripts/build
iojs out/tests.js
----

You will need to have nodejs or iojs installed on your system.


=== License

_promesa_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2015 Andrey Antukh <niwi@niwi.nz>

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
