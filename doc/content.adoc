= promesa - composable promise library for ClojureScript
Andrey Antukh, <niwi@niwi.nz>
0.5.1
:toc: left
:!numbered:
:idseparator: -
:idprefix:
:sectlinks:
:source-highlighter: pygments
:pygments-style: friendly


== Introduction

A promise library for Clojure and ClojureScript.

On the JVM platform is built on top of *completable futures* (requires jdk8)
and on JS platform is built on top of
https://github.com/petkaantonov/bluebird/[bluebird] promise library.


=== Project Maturity

Since _promesa_ is a young project there may be some API breakage.


=== Install

Just include that in your depencency vector on *_project.clj_*:

[source,clojure]
----
[funcool/promesa "0.5.1"]
----

This package requires JDK8 if you are using it on the JVM platform and
all enviroments that *bluebird* library supports on the JS platform.


== User guide

=== Introduction

The promise is the abstraction that represents the result of an asynchronous
operation that will be eventually available.

It can be on this states:

- `resolved`: means that the promise contains a value.
- `rejected`: means thet the promise contains an error.
- `pending`: means that the promise does not have value.


=== Creating a promise

It there several different ways to create a promise in _promesa_ library. You can
create it already resolved with initial value or already rejected with an exception.

Let start creating a promise from a plain value:

[source, clojure]
----
(require '[promesa.core :as p])

;; Create a fulfilled promise
(p/promise 1)
;; => #<Promise [1]>
----

If a `promise` function receives a plain value, it returns a resolved promise with
the provided plain value. If it receives an instance of `Error`, it returns a
rejected promise.

Also, it accepts a factory callback that receives two callable parameters:
`resolve` and `reject`. So you can use one or other to resolve or reject the promise
respectively.

[source, clojure]
----
;; Create a promise from callback
(p/promise (fn [resolve reject]
             ;; Do things, maybe async...
             (resolve 1)))
----

[NOTE]
====
On JVM platform the function is executed asynchronously in an other thread and
in JS platform is determined by *bluebird* default behavior.
====


=== Simple chaining

It there different ways to compose/chain computations using promises. We will start
with the basic: lineal way of chaining computations.

That can be done using `then` functions exposed in `promesa.core` namespace:

[source, clojure]
----
(def pr (-> (p/promise 1)
            (p/then inc)
            (p/then inc)
            (p/then inc)))

(p/then pr (fn [v]
             (println v)))
;; => Stdout: 4
----

The same example can be written in this shorter way:

[source, clojure]
----
(def pr (-> (p/promise 1)
            (p/then inc inc inc))
(p/then pr (fn [v]
             (println v)))

;; => Stdout: 4
----

This library also plays in the link:https://github.com/funcool/cats[cats] library
protocols so you can use the *Functor* interface for apply a function to a promise
value:

[source, clojure]
----
(require '[cats.core :as m])

(-> (m/fmap inc (p/promise 10))
    (p/then (fn [v]
              (println v))))

;; => Stdout: 11
----


=== Advanced chaining

Until now, we have seen a very simple promise compositions that does not depends
on previous results. The `cats.core/mlet` and `cats.core/alet`  macro allows us
build more complex and more powerfull promise compositions.

For demostration purposes, imagine that you have this function that emulates async
operation and return a promise:

[source, clojure]
----
(require '[cats.core :as m])
(require '[promesa.core :as p])

(defn sleep-promise
  [wait]
  (p/promise (fn [resolve reject]
               (p/schedule wait #(resolve wait)))))
----

Let's see an example using `mlet` macro:

[source, clojure]
----
(def pr
  (m/mlet [x (sleep-promise 42)
           y (sleep-promise 41)]
    (+ x y)))

(p/then pr (fn [v]
             (println v)))

;; This will take aprox: 83-85 msecs
;; => Stdout: 83
----

The `mlet` bindings are executed sequentially, waiting in each step for promise
resolution. If an error occurs in some step, the entire composition will be
short-circuited, returing exceptionally resolved promise. This is very similar
to the up coming link:http://jakearchibald.com/2014/es7-async-functions/[ES7 async/await], but built in a generic way and as a library and not as language feature.

The main disadvantage of `mlet` is that it's evaluation model is strictly
secuential. It is ok for some use cases, when the sequential order is mandatory.

But, if the strictly secuential model is not mandatory, `alet` macro can be used
as drop-in-replacement for `mlet` that automatically exploits the concurrency:

[source, clojure]
----
(def pr
  (m/alet [x (sleep-promise 42)
           y (sleep-promise 41)]
    (+ x y)))

(p/then pr (fn [v]
             (println v)))

;; This will take aprox: 41-45 msecs
;; => Stdout: 83
----

You can observe that the return value is identical to the previous example,
but it takes almost half of time to finish. That is because `alet` is more
smarter macro. It calculates the dependencies between declared bindings and
executes them in batches.

You can read more about that
link:http://funcool.github.io/cats/latest/#syntax-sugar[here].


=== Error handling

One of the advantages of using promise abstraction is that it natively has
a notion of error, so you don't need reinvent it. If some of the computations
of the composed promise chain/pipeline raises an exception, that one is
automatically propagated to the last promise making the effect of short-circuiting.

Let see an example:

[source, clojure]
----
(-> (p/promise (ex-info "error" nil))
    (p/catch (fn [error]
               (.log js/console error))))
----

The `catch` function adds a new handler to the promise chain that will be called
when any of the previous promises in the chain are rejected or an exception is
raised. The `catch` function also returns a promise that will be resolved or
rejected depending on that will happen inside the catch hanlder.

[NOTE]
====
On the JVM platform the reject value is mandatory to be an instance of `Throwable`
but in JS platform it can by any value.
====


=== Branching

For adding both success and error handlers to a promise at the same time you can use
the `branch` function:

[source, clojure]
----
(p/branch a-promise
          (fn [v]
            (println "Ok" v))
          (fn [err]
            (println err)))
----


=== Working with collections

In some circumstances you will want wait a completion of few promises at same time,
and _promesa_ also provides helpers for that.

Imagine that you have a collection of promises and you want to wait until
all of them are resolved. This can be done using the `all` combinator:

[source, clojure]
----
(let [p (p/all [(do-some-io)
                (do-some-other-io)])]
  (p/then p (fn [[result1 result2]
              (do-something-with-results result1 result2))))
----

It there are also circumstances where you only want arbitrary select of the
first resolved promise. For this case, you can use the `any` combinator:

[source, clojure]
----
(let [p (p/any [(p/delay 100 1)
                (p/delay 200 2)
                (p/delay 120 3)])]
  (p/then p (fn [x]
              (.log js/console "The first one finished: " x))))
----

Later, for more advanced use cases, _promesa_ is an algebraic structure that
implements the associative binary operation usually called `mappend`:

[source, clojure]
----
(require '[cats.core :as m])

(def pr (m/mappend (p/promise {:a 1})
                   (p/promise {:b 2})))
(p/then pr (fn [v]
             (println v)))

;; => Stdout: {:a 1 :b 2}
----


=== Delays and timeouts.

JavaScript due its nature, does not allow you to block or sleep. But with promises
you can emulate the functionality using `delay` like so:

[source, clojure]
----
(-> (p/delay 1000 "foobar")
    (p/then (fn [v]
              (println "Received:" v))))

;; After 1 second it will print the message
;; to the console: "Received: foobar"
----

The promise library offers the ability to add a timeout to async operations thanks
to the `timeout` function:

[source, clojure]
----
(-> (some-async-task)
    (p/timeout 200)
    (p/then #(println "Task finished" %))
    (p/catch #(println "Timeout" %)))
----

In case the async task is slow, in the example more that 200ms, the promise will be
rejected with timeout error and successfully captured with the `catch` handler.


== Developers Guide

=== Contributing

Unlike Clojure and other Clojure contrib libs, does not have many restrictions for
contributions. Just open a issue or pull request.


=== Get the Code

_promesa_ is open source and can be found on
link:https://github.com/funcool/promesa[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/promesa
----


=== Run tests

To run the tests execute the following:

For the JVM platform:

[source, text]
----
lein test
----

And for JS platform:

[source, text]
----
./scripts/build
node out/tests.js
----

You will need to have nodejs installed on your system.


=== License

_promesa_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2015 Andrey Antukh <niwi@niwi.nz>

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
